def driverProg():
  textmsg("Program starting.")
  MSG_QUIT = 1
  MSG_STOPJ = 3
  MSG_SERVOJ = 5
  MSG_SET_TEACH_MODE = 7
  MSG_SET_PAYLOAD = 8
  MSG_SET_SPEED = 15
  MULT = 10000.0
  MASS_DIVISOR = 1000.0
  COORD_DIVISOR = 1000.0
  SPEED_DIVISOR = 1000.0
  SERVO_IDLE = 0
  SERVO_RUNNING = 1
  TEACH_MODE = 2
  state = SERVO_IDLE
  commanded_positions = get_actual_joint_positions()
  def set_commanded_positions(q):
    enter_critical
    # while in TEACH_MODE, the servo commands will be ignored
    if state != TEACH_MODE:
      state = SERVO_RUNNING
    end
    commanded_positions = q
    exit_critical
  end
  def set_speed_scale(speed):
    # open socket to send the command to set the speed percentage
    socket_open("127.0.0.1", 30002, "socket_1")
    socket_send_string("set speed", "socket_1")
    socket_send_string(speed, "socket_1")
    socket_send_byte(10, "socket_1")
    socket_close("socket_1")
  end
  thread servoThread():
    while True:
      enter_critical
      q = commanded_positions
      exit_critical
      
      if state == SERVO_RUNNING:
        enter_critical
        # here we check again, as the state could have changed to teach mode while running the servoj
        if state != TEACH_MODE:
          state = SERVO_IDLE
        end
        exit_critical
        servoj(q, 0, 0, 0.008)
      else:
        sync()
      end
    end
  end

  # set default speed
  set_speed_scale(0.5)

  # open socket to receive commands from host
  socket_open("%s", %d)
  socket_send_string("Connected")
  thread_servo = run servoThread()
  # default payload: nothing attached to the arm
  set_payload(0.0, [0.0, 0.0, 0.0])
  while True:
    telegram = socket_read_binary_integer(7)
    if telegram[0] == 7:
      msg_type = telegram[1]
      if msg_type == MSG_QUIT:
        textmsg("Quit command received.")
        socket_send_string("Quit")
        break
      elif msg_type == MSG_SERVOJ:
        q = [telegram[2]/MULT,
             telegram[3]/MULT,
             telegram[4]/MULT,
             telegram[5]/MULT,
             telegram[6]/MULT,
             telegram[7]/MULT]
        set_commanded_positions(q)
      elif msg_type == MSG_STOPJ:
        textmsg("Stop command received.")
        socket_send_string("Stop")
        stopj(1.0)
      elif msg_type == MSG_SET_TEACH_MODE:
        if telegram[2] != 0:
          textmsg("Enabling teach mode.")
          enter_critical
          state = TEACH_MODE
          exit_critical
          kill thread_servo
          teach_mode()
          socket_send_string("Teach mode ON")
        else:
          textmsg("Disabling teach mode.")
          end_teach_mode()
          kill thread_servo
          enter_critical
          state = SERVO_IDLE
          exit_critical
          thread_servo = run servoThread()
          socket_send_string("Teach mode OFF")
        end
      elif msg_type == MSG_SET_PAYLOAD:
        mass = telegram[2]/MASS_DIVISOR
        coords = [telegram[3]/COORD_DIVISOR,
                  telegram[4]/COORD_DIVISOR,
                  telegram[5]/COORD_DIVISOR]
        textmsg("Setting payload mass to ", mass)
        textmsg("Setting payload coordinates to ", coords)
        set_payload(mass, coords)
        socket_send_string("Payload set")
      elif msg_type == MSG_SET_SPEED:
        speed = telegram[2]/SPEED_DIVISOR
        textmsg("Setting speed to ", speed)
        set_speed_scale(speed)
        socket_send_string("Speed set")
      else:
        textmsg("Unknown message type received.")
        socket_send_string("Error")
      end
    end
  end
  textmsg("Program exiting.")
  socket_close()
end
